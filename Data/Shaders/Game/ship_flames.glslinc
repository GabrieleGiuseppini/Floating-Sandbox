###VERTEX-120

#define in attribute
#define out varying

// Inputs
in vec4 inFlame1; // Position (vec2), PlaneId, PersonalitySeed
in vec3 inFlame2; // WindRotationAngle, FlameSpacePosition

// Outputs
out float windRotationAngle;
out vec2 flameSpacePosition;
out vec2 noiseOffset;

// Params
uniform mat4 paramOrthoMatrix;
uniform float paramFlameProgress;

void main()
{
    windRotationAngle = inFlame2.x;
    flameSpacePosition = inFlame2.yz;
    noiseOffset = vec2(inFlame1.w, inFlame1.w - paramFlameProgress);

    gl_Position = paramOrthoMatrix * vec4(inFlame1.xyz, 1.0);
}

###FRAGMENT-120

#define in varying

// Inputs from previous shader
in float windRotationAngle; // [-0.5, +0.5]
in vec2 flameSpacePosition; // (x=[-1.0, 1.0], y=[0.0, 1.0])
in vec2 noiseOffset;

// The textures
uniform sampler2D paramNoiseTexture;
uniform sampler2D paramGenericLinearTexturesAtlasTexture;

// Params
uniform vec2 paramAtlasTile1Dx; // span across two pixels
uniform vec2 paramAtlasTile1LeftBottomTextureCoordinates;
uniform vec2 paramAtlasTile1Size;
uniform float paramFlameProgress;

//
// Loosely based on "Flame in the Wind" by kuvkar (https://www.shadertoy.com/view/4tXXRn)
//

mat2 GetRotationMatrix(float angle)
{
    mat2 m;
    m[0][0] = cos(angle); m[0][1] = -sin(angle);
    m[1][0] = sin(angle); m[1][1] = cos(angle);

    return m;
}

// -----------------------------------------------
void main()
{   
    // Fragments with alpha lower than this are discarded
    #define MinAlpha 0.2

    //
    // Apply wind
    //
    
    // Rotation angle is higher the higher we go
    float windAngle = windRotationAngle * flameSpacePosition.y;
    windAngle += (noiseOffset.x - .5) * windAngle * 1.6; // Randomize with personality seed, +/- 80%
            
    // Rotate around bottom
    vec2 uv = GetRotationMatrix(windAngle) * flameSpacePosition;
    
    // Center vertically
    uv -= vec2(0.0, 0.5);

    // uv now is in the (x=[-1.0, 1.0], y=[-0.5, 0.5]) range


    //
    // Get noise for this fragment and time
    //

    #define NoiseResolution 0.4
    vec2 noiseUv = uv * NoiseResolution + noiseOffset;
    float fragmentNoise = texture2D(paramNoiseTexture, noiseUv).r * 0.5 + 0.25; // -> (0.25, 0.75)
    
    
    //
    // Rotate fragment based on noise and vertical extent
    //
    
    float angle = (fragmentNoise - 0.5); // -> (-0.25, 0.25)
        
    // Magnify rotation amount based on distance from center of screen
    angle /= max(0.1, length(uv));
        
    // Straighten the flame at the bottom and make full turbulence higher up
    angle *= smoothstep(-1.0, 0.3, flameSpacePosition.y);    
    
    // Smooth the angle
    angle *= 0.35;
    
    // Rotate and add!
    uv += GetRotationMatrix(angle) * uv;    
        
    // uv now is in the (x=[-2.0, 2.0], y=[-1.0, 1.0]) range (really?)
    
    //
    // Calculate thickness
    //

    #define FlameWidth 2.4 // The higher this value, the narrower the flame
    float thickness = 1.3 - abs(uv.x) * FlameWidth;
    
    // Taper flame down
    thickness *= smoothstep(-0.15, 0.15, flameSpacePosition.y); // Taper down    

    // Calculate implicit alpha
    float implicitAlpha = smoothstep(0.7, 1.2, thickness);

    if (implicitAlpha < MinAlpha) // For perf
        discard;   
   
    //
    // Sample alpha
    //

    #define AlphaResolution 2.0 // When >= 2, we're guaranteed to fall within the atlas tile's boundaries

    vec2 alphaVirtualTextureCoords = vec2(
            ((uv.x + 1.0) + noiseOffset.x) / AlphaResolution,
            min(flameSpacePosition.y, 1.0 - paramAtlasTile1Dx.y)); // y has to fit perfectly in the 0..1 range, taking into account the dead-center dy

    vec2 alphaSampleCoords = paramAtlasTile1LeftBottomTextureCoordinates + paramAtlasTile1Size * alphaVirtualTextureCoords;
    vec4 sampledAlphaSample = texture2D(paramGenericLinearTexturesAtlasTexture, alphaSampleCoords);

    // We only want sampled alpha to affect the top part of the flame; the bottom
    // part should stay put at 1.0
    float sampledAlpha = mix(1.0, sampledAlphaSample.w, flameSpacePosition.y * flameSpacePosition.y);
    
    //
	// Sample color
    //

    #define ColorResolution 7.

    vec2 colorVirtualTextureCoords = fract(
        vec2(
            uv.x / ColorResolution,
            uv.y / ColorResolution - paramFlameProgress * 1.5 + noiseOffset.x / 3.)); // Adding some per-flame random Y displacement

    // Clamp to dead center pixels
    colorVirtualTextureCoords = clamp(colorVirtualTextureCoords, paramAtlasTile1Dx, vec2(1.0) - paramAtlasTile1Dx);

    vec2 colorSampleCoords = paramAtlasTile1LeftBottomTextureCoordinates + paramAtlasTile1Size * colorVirtualTextureCoords;
    vec4 sampledColor = texture2D(paramGenericLinearTexturesAtlasTexture, colorSampleCoords);

    // Better contrast
    float lum = 0.2126 * sampledColor.r + 0.7152 * sampledColor.g + 0.0722 * sampledColor.b;
    float flum = 1.0 / (3.0 + exp(8.0 - 16.0*lum*lum));
    sampledColor = vec4(
        sampledColor.rgb + (vec3(1.) - sampledColor.rgb) * flum,
        sampledColor.a);

    //
    // Emit
    //

    // Calculate final alpha
    float finalAlpha = sampledAlpha * implicitAlpha;
    
    if (finalAlpha < MinAlpha) // For Z test
        discard;

    // Make final color
    gl_FragColor = vec4(sampledColor.xyz, finalAlpha);


    /*
    //
    // Test for tile wrapping
    //

    vec2 srCoords = (flameSpacePosition + vec2(1.0, 0.0)) * vec2(0.5, 1.0); // [0.0, 1.0] [0.0, 1.0]
    srCoords.y = fract(srCoords.y - paramFlameProgress); // Wrap
    srCoords.y = clamp(srCoords.y, paramAtlasTile1Dx.y, 1.0 - paramAtlasTile1Dx.y); // Clamp to fight against linear filtering - though no idea why dx works and dx/2.0 does not work
    vec2 sampleCoords = paramAtlasTile1LeftBottomTextureCoordinates + paramAtlasTile1Size * srCoords;
    vec4 testCol = texture2D(paramGenericLinearTexturesAtlasTexture, sampleCoords);
    gl_FragColor = vec4(testCol.xyz, 1.0);
    */
}