###VERTEX-120

#define in attribute
#define out varying

#ifdef IS_UPPER

// Inputs
in vec2 inOceanDetailed1Upper;	// Position (vec2)
in vec4 inOceanDetailed2Upper;	// yBack/yMid/yFront/dYFront2 (float, world y)

// Parameters
uniform mat4 paramOrthoMatrix;

// Outputs
out vec2 shadowPosNdc;
out float yWorld;
out vec4 yVectorWorld; // yBackWorld/yMidWorld/yFrontWorld/dYFront2World

void main()
{    
    // Calculate position
    vec4 posNdc = paramOrthoMatrix * vec4(inOceanDetailed1Upper.xy, -1.0, 1.0);
    gl_Position = posNdc;

    // Pass values to fragment shader
    shadowPosNdc = vec2(posNdc.x, posNdc.y - paramOrthoMatrix[3][1]);
    yWorld = inOceanDetailed1Upper.y;
    yVectorWorld = inOceanDetailed2Upper;
}

#else

// Inputs
in vec2 inOceanDetailed1Lower;	// Position (vec2)

// Parameters
uniform mat4 paramOrthoMatrix;

// Outputs
out vec2 shadowPosNdc;
out float yWorld;

void main()
{    
    // Calculate position
    vec4 posNdc = paramOrthoMatrix * vec4(inOceanDetailed1Lower.xy, -1.0, 1.0);
    gl_Position = posNdc;

    // Pass values to fragment shader
    shadowPosNdc = vec2(posNdc.x, posNdc.y - paramOrthoMatrix[3][1]);
    yWorld = inOceanDetailed1Lower.y;
}

#endif

###FRAGMENT-120

#define in varying

#include "common.glslinc"
#include "lamp_tool.glslinc"
#include "ocean.glslinc"

// Inputs from previous shader
in vec2 shadowPosNdc;
in float yWorld;
#ifdef IS_UPPER
in vec4 yVectorWorld; // yBackWorld/yMidWorld/yFrontWorld/dYFront2World
#endif

// Input textures
uniform sampler2D paramNoiseTexture;
uniform sampler1D paramSharedTexture;

// Parameters
uniform float paramEffectiveAmbientLightIntensity;
uniform vec3 paramEffectiveMoonlightColor;
uniform float paramOceanTransparency;
uniform vec3 paramOceanDepthColorStart;
uniform vec3 paramOceanDepthColorEnd;
uniform float paramOceanDepthDarkeningRate;
uniform float paramSunRaysInclination; // 0.0==vertical, 1.0/-1.0==45/-45 degrees
#ifdef IS_UPPER
uniform float paramWorldPerPixel;
#endif

void main()
{
    // Calculate depth darkening
    float darkeningFactor = CalculateOceanDepthDarkeningFactor(
        yWorld,
        paramOceanDepthDarkeningRate);

    // Calculate lamp tool intensity
    float lampToolIntensity = CalculateLampToolIntensity(gl_FragCoord.xy);

    // Apply gradient
    vec3 depthColor = mix(
        paramOceanDepthColorStart,
        paramOceanDepthColorEnd,
        darkeningFactor * (1.0 - lampToolIntensity));

    // Apply dither
    depthColor = ApplyDither(
        depthColor,
        gl_FragCoord.xy,
        paramNoiseTexture);
    
    // Apply shadows
    float shadowFactor = GetShadowFactor(
        shadowPosNdc,
        yWorld,
        paramSunRaysInclination,
        paramSharedTexture,
        paramNoiseTexture);
    depthColor = depthColor * shadowFactor;

    // Apply ambient light
    depthColor = ApplyAmbientLight(
        depthColor, 
        paramEffectiveMoonlightColor * 0.5, 
        paramEffectiveAmbientLightIntensity, 
        lampToolIntensity);

#ifdef IS_UPPER
    // Apply detail
    gl_FragColor = CalculateDetailedOceanPlaneColorUpper(
        vec4(depthColor, (1.0 - paramOceanTransparency)),
        yWorld,
        yVectorWorld,
        paramWorldPerPixel,
        paramEffectiveMoonlightColor,
        paramEffectiveAmbientLightIntensity);
#else
    // Apply detail
    gl_FragColor = vec4(depthColor, (1.0 - paramOceanTransparency));
#endif
}
